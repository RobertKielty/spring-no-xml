project.ext {
    springVersion = "4.0.2.RELEASE"
    cucumberJvmVersion = "1.1.7"
    junitVersion = '4.11'
    hamcrestVersion = '1.3'
    cargoVersion = '1.4.5'
    handleId = 'tomcat7'
}

apply plugin: "java"
sourceCompatibility = 1.7
group = "com.test"
version = "1.0"

configurations {
    provided

    cucumberRuntime {
        extendsFrom testRuntime
    }

    cucumber {
        dependencies {
            cucumber "info.cukes:cucumber-core:$project.cucumberJvmVersion"
            cucumber "info.cukes:cucumber-java:$project.cucumberJvmVersion"
            cucumber("info.cukes:cucumber-junit:$project.cucumberJvmVersion") {
                exclude group: 'junit', module: 'junit'
            }
        }
    }

    cargoDeps {
        dependencies {
            cargoDeps "org.codehaus.cargo:cargo-core-uberjar:$project.cargoVersion"
            cargoDeps "org.codehaus.cargo:cargo-ant:$project.cargoVersion"
        }
    }

    testing {
        dependencies {
            testing "com.jayway.restassured:rest-assured:2.3.1"
            testing "org.mockito:mockito-all:1.9.5"
            testing "junit:junit-dep:$project.junitVersion"
            testing "org.hamcrest:hamcrest-core:$project.hamcrestVersion"
            testing "org.hamcrest:hamcrest-library:$project.hamcrestVersion"
        }
    }
}

apply plugin: "war"

apply plugin: "idea"

idea {
    project {
        jdkName = '1.7'
        languageLevel = '1.7'
        ipr {

            withXml { provider ->
                provider.node.component.find { it.@name == 'VcsDirectoryMappings' }.mapping.@vcs = 'Git'
            }
        }
    }

    module {

        scopes.PROVIDED.plus += configurations.provided

        downloadJavadoc = false
        downloadSources = true
    }
}

apply plugin: 'eclipse'

repositories {
    mavenCentral()
}

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.gradle.api.plugins:gradle-cargo-plugin:1.5'
    }
}

apply plugin: 'cargo'
apply plugin: 'cargo-base'

dependencies {
    compile "org.springframework:spring-webmvc:$project.springVersion"
    compile "javax.servlet:javax.servlet-api:3.0.1"
    testCompile "org.springframework:spring-test:$project.springVersion"

    testCompile configurations.cucumber
    testCompile configurations.testing

    cargo configurations.cargoDeps
}

cargo {
    containerId = 'tomcat7x'
    port = 8181

    deployable {
        context = '/'
    }

    local {

        installer {
            installUrl = 'http://apache.osuosl.org/tomcat/tomcat-7/v7.0.54/bin/apache-tomcat-7.0.54.zip'
            downloadDir = file("$buildDir/download")
            extractDir = file("$buildDir/extract")
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = 1.11
}

test {
    systemProperty "file.encoding", "UTF-8"
    systemProperty "java.awt.headless", "true"
    include "com/**"
    exclude "com/test/cukes/**"
}

task acceptanceTestsLocal(type: Test) {
    dependsOn assemble, cargoStartLocal, compileTestJava

    include "com/test/cukes/**"
//    doLast {
//        javaexec {
//            main = "cucumber.api.cli.Main"
//            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
//            args = ['-f', 'html:build/reports/tests/acceptance/local/html',
//                    '-f', 'json:build/reports/tests/acceptance/local/json/index.json',
//                    '-g','com/test/cukes/steps', 'src/test/resources',
//                    ]
//            jvmArgs = []
//        }
//    }

    finalizedBy cargoStopLocal
}

task integrationTests(type: Test) {
    include "**/*IT.*"
    exclude "com/test/cukes/**"
}

